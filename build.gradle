buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.6'
        classpath "com.avast.gradle:docker-compose-gradle-plugin:0.3.22"
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply from: "./libraries.gradle"
apply plugin: 'com.bmuschko.docker-java-application'
apply plugin: 'docker-compose'
apply from: "./gradle/integTest.gradle"


group 'com.qivicon.backend.messaging'
version '1.0.0-SNAPSHOT'

mainClassName = 'com.qivicon.backend.messaging.ApplicationLauncher'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

tasks.withType(JavaCompile) {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked"
}

repositories {
    mavenCentral()
}

dependencies {
    compile ( libraries.vertx_core)
    compile ( libraries.vertx_metrics)
    compile ( libraries.vertx_web)
    compile ( libraries.vertx_config)
    compile ( libraries.vertx_health_check)
    compile ( libraries.rabbitmq_client)
    compile ( libraries.logback)
    compile ( libraries.prometheus)
    compile ( libraries.prometheus_dropwizard)

    testCompile ( libraries.junit)
    testCompile ( libraries.assertj)
    testCompile ( libraries.mockito)
    testCompile ( libraries.vertx_unit)
    testCompile ( libraries.testcontainers )
    testCompile ( libraries.okhttp )
    testCompile ( libraries.concurrentunit )
}

shadowJar {
    classifier = 'fat'
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

run {
    args = ['run', "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

docker {
    javaApplication {
        baseImage = 'openjdk:8-jre-alpine'
        maintainer = 'Robert Winkler'
        ports = [8080]
        tag = 'messaging-proxy:latest'
    }
}

composeUp.dependsOn dockerBuildImage
dockerCompose.isRequiredBy(integTest)

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}